
`____________________________________________________________________________[$69E9]___
`OBJECT: wooden platform (Green Hill)

PROC    :doObjectCode_platform                  SECTION ::objects {
	set	5,(ix+$18)
	
	ld	(ix+#object.width), 26
	ld	(ix+#object.height),16
	ld	(ix+#object.spriteLayout.lo),:object_platform_spriteLayouts._6911.lo
	ld	(ix+#object.spriteLayout.hi),:object_platform_spriteLayouts._6911.hi
	
	ld	a,($.SONIC.Ydirection)
	and	a
	jp	m,._2
	
	ld	hl,$0806
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._2
	
	ld	de,$0000
	
	ld	a,(ix+#object.Y.lo)
	and	%00011111		        `MOD 32
	cp	$10
	jr	nc,._1
	
	ld	e,$80
._1	ld	(ix+#object.Yspeed.lo),e
	ld	(ix+#object.Yspeed.hi),d
	ld	(ix+#object.Ydirection),$00
	ld	bc,$0010
	call	:_LABEL_7CC1_12
	ret
	
._2	ld	c,$00
	ld	l,c
	ld	h,c
	ld	a,(ix+#object.Y.lo)
	and	%00011111
	jr	z,._3
	
	ld	hl,$ffc0
	dec	c
._3	ld	(ix+#object.Yspeed.lo),l
	ld	(ix+#object.Yspeed.hi),h
	ld	(ix+#object.Ydirection),c
	ret
}