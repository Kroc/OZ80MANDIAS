`` Sonic 1 Master System Disassembly
   created by Kroc Camen <kroc@camendesign.com>
   for MaSS1VE: The Master System Sonic 1 Visual Editor <github.com/Kroc/MaSS1VE>
   ================================================================================= ``

`NOTE: the power-ups share code between themselves

SECTION ::object_powerUps BANK 1, 2 SLOT 1, 2 {

	`____________________________________________________________________[$5B09]___
	`OBJECT: monitor - rings

PROC	:doObjectCode_powerUp_ring {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._1
	call	:_5deb
	jr	c,._1
._5b24
	ld	a,$10
	call	:_39ac
._5b29
	xor	a			`set A to 0
	ld	(ix+#object.spriteLayout+0),a
	ld	(ix+#object.spriteLayout+1),a
	ret	
	
._1	ld	hl,$5180		`$15180 - blinking items art
._5b34
	call	:loadPowerUpIcon
	ld	(ix+#object.spriteLayout+0),:_5bbf.lo
	ld	(ix+#object.spriteLayout+1),:_5bbf.hi
	ld	a,($.FRAMECOUNT)
	and	$07
	cp	$05
	ret	nc
	ld	(ix+#object.spriteLayout+0),:_5bcc.lo
	ld	(ix+#object.spriteLayout+1),:_5bcc.hi
	ld	l,(ix+$01)
	ld	h,(ix+#object.X+0)
	ld	a,(ix+#object.X+1)
	ld	e,(ix+#object.Xspeed+0)
	ld	d,(ix+#object.Xspeed+1)
	add	hl,de
	adc	a,(ix+#object.Xdirection)
	ld	l,h
	ld	h,a
	ld	($.TEMP1),hl
	ld	l,(ix+$04)
	ld	h,(ix+#object.Y+0)
	ld	a,(ix+#object.Y+1)
	bit	7,(ix+$18)
	jr	nz,._2
	ld	e,(ix+#object.Yspeed+0)
	ld	d,(ix+#object.Yspeed+1)
	add	hl,de
	adc	a,(ix+#object.Ydirection)
._2	ld	l,h
	ld	h,a
	ld	($.TEMP3),hl
	ld	hl,$0004
	ld	($.TEMP4),hl
	ld	hl,$0000
	ld	($.TEMP6),hl
	ld	a,$5c
	call	:_3581
	ld	hl,$000c
	ld	($.TEMP4),hl
	ld	a,$5e
	call	:_3581
	bit	1,(ix+$18)
	ret	z
	ld	l,(ix+#object.Yspeed+0)
	ld	h,(ix+#object.Yspeed+1)
	ld	a,(ix+#object.Ydirection)
	ld	de,$0040
	add	hl,de
	adc	a,$00
	ld	(ix+#object.Yspeed+0),l
	ld	(ix+#object.Yspeed+1),h
	ld	(ix+#object.Ydirection),a
	ret

	}

:_5bbf
	DATA $54, $56, $58, $FF, $FF, $FF
	DATA $AA, $AC, $AE, $FF, $FF, $FF
	DATA $FF
:_5bcc
	DATA $54, $FE, $58, $FF, $FF, $FF
	DATA $AA, $AC, $AE, $FF, $FF, $FF
	DATA $FF

	`____________________________________________________________________[$5BD9]___
	`OBJECT: monitor - speed shoes

PROC	:doObjectCode_powerUp_speed {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._1
	call	:_5deb
	jr	c,._1
	ld	a,$f0
	ld	($D411),a
	ld	a,$02
	rst	:rst_playSFX
	jp	:doObjectCode_powerUp_ring._5b29
	
._1	ld	hl,$5200
	jp	:doObjectCode_powerUp_ring._5b34

	}

	`____________________________________________________________________[$5C05]___
	`OBJECT: monitor - life

PROC	:doObjectCode_powerUp_life {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	
	`check if the level has its bit flag set at $D305+
	ld	hl,$D305
	call	:getLevelBitFlag
	ld	a,(hl)
	and	c
	jr	z,._1			`if not set, skip ahead
	
	ld	(ix+#object.type),$FF	`remove object?
	jp	:doObjectCode_powerUp_ring._5b29
	
._1	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._2
	call	:_5deb
	jr	c,._2
	bit	2,(ix+$18)
	jp	nz,:doObjectCode_powerUp_ring._5b24
	ld	hl,$.LIVES
	inc	(hl)
	
	`set the level's bit flag at $D305+
	ld	hl,$D305
	call	:getLevelBitFlag
	ld	a,(hl)
	or	c
	ld	(hl),a
	
	xor	a			`set A to 0
	ld	(ix+#object.spriteLayout+0),a
	ld	(ix+#object.spriteLayout+1),a
	
	ld	a,$09
	rst	:rst_playSFX
	
	ld	a,($.CURRENT_LEVEL)
	cp	28			`special stage?
	ret	nc
	
	ld	hl,$D280
	inc	(hl)
	ret
	
	`------------------------------------------------------------------------------
._2	ld	a,($.CURRENT_LEVEL)
	cp	4			`level 4 (Bridge Act 2)?
	jr	z,._4
	cp	$09			`level 9 (Labyrinth Act 1)?
	jr	z,._6
	cp	$0c			`level 12 (Scrap Brain Act 1)?
	jr	z,._7
	cp	$11			`level 11 (Labyrinth Act 3)?
	jr	z,._8
	
._3	ld	hl,$5280
	jp	:doObjectCode_powerUp_ring._5b34

._4	ld	c,$00
	ld	de,$0040
	ld	a,(ix+$13)
	cp	$3c
	jr	c,._5
	dec	c
	ld	de,$ffc0
._5	ld	(ix+#object.Yspeed+0),e
	ld	(ix+#object.Yspeed+1),d
	ld	(ix+#object.Ydirection),c
	inc	(ix+$13)
	ld	a,(ix+$13)
	cp	$50
	jr	c,._3
	ld	(ix+$13),$28
	jr	._3
	
._6	set	2,(ix+$18)
	ld	hl,$D317
	call	:getLevelBitFlag
	ld	a,(hl)
	ld	hl,$5180
	and	c
	jp	z,:doObjectCode_powerUp_ring._5b34
	res	2,(ix+$18)
	ld	hl,$5280
	jp	:doObjectCode_powerUp_ring._5b34
	
._7	set	1,(ix+$18)
	ld	(ix+#object.Xspeed+0),$80
	ld	(ix+#object.Xspeed+1),$00
	ld	(ix+#object.Xdirection),$00
	jr	._3
	
._8	ld	a,($D280)
	cp	$11
	jr	nc,._3
	ld	(ix+#object.type),$FF	`remove object?
	jr	._3

	}

	`____________________________________________________________________[$5CD7]___
	`OBJECT: monitor - shield

PROC	:doObjectCode_powerUp_shield {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._1
	call	:_5deb
	jr	c,._1
	set	5,(iy+#vars.flags6)
	jp	:doObjectCode_powerUp_ring._5b29
	
._1	ld	hl,$5300
	jp	:doObjectCode_powerUp_ring._5b34

	}

	`____________________________________________________________________[$5CFF]___
	`OBJECT: monitor - invincibility

PROC	:doObjectCode_powerUp_invincibility {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._1
	call	:_5deb
	jr	c,._1
	set	0,(iy+#vars.unknown0)
	ld	a,$f0
	ld	($.D28D),a
	
	ld	a,#index_music_invincibility
	rst	:rst_playMusic
	
	jp	:doObjectCode_powerUp_ring._5b29
	
._1	ld	hl,$5380
	jp	:doObjectCode_powerUp_ring._5b34

	}

	`____________________________________________________________________[$5D2F]___
	`OBJECT: monitor - checkpoint

PROC	:doObjectCode_powerUp_checkpoint {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._1
	call	:_5deb
	jr	c,._1
	
	ld	hl,$D311
	call	:getLevelBitFlag
	ld	a,(hl)
	or	c
	ld	(hl),a
	
	ld	a,($.CURRENT_LEVEL)
	add	a,a
	ld	e,a
	ld	d,$00
	ld	hl,$D32E
	add	hl,de
	ex	de,hl			`DE is $D32E + level number * 2
	ld	l,(ix+#object.X+0)
	ld	h,(ix+#object.X+1)
	add	hl,hl
	add	hl,hl
	add	hl,hl
	ld	a,h
	ld	(de),a
	inc	de
	ld	l,(ix+#object.Y+0)
	ld	h,(ix+#object.Y+1)
	add	hl,hl
	add	hl,hl
	add	hl,hl
	ld	a,h
	dec	a
	ld	(de),a
	jp	:doObjectCode_powerUp_ring._5b29
	
._1	ld	hl,$5480
	jp	:doObjectCode_powerUp_ring._5b34

	}
	
	`____________________________________________________________________[$5D80]___
	`OBJECT: monitor - continue

PROC	:doObjectCode_powerUp_continue {
	
	ld	(ix+#object.width),$14
	ld	(ix+#object.height),$18
	call	:_5da8
	ld	hl,$0003
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._1
	call	:_5deb
	jr	c,._1
	set	3,(iy+#vars.flags9)
	jp	:doObjectCode_powerUp_ring._5b29
	
._1	ld	hl,$5500
	jp	:doObjectCode_powerUp_ring._5b34

	}

	`--------------------------------------------------------------------[$5DA8]---

PROC	:_5da8 {
	
	bit	0,(ix+$18)
	ret	nz
	ld	a,($.LEVEL_SOLIDITY)
	and	a
	jr	nz,._1
	ld	bc,$0000
	ld	e,c
	ld	d,b
	call	:getFloorLayoutRAMPositionForObject
	ld	de,$0016
	ld	bc,$0012
	ld	a,(hl)
	cp	$ab
	jr	z,._2
._1	ld	de,$0004
	ld	bc,$0000
._2	ld	l,(ix+#object.X+0)
	ld	h,(ix+#object.X+1)
	add	hl,de
	ld	(ix+#object.X+0),l
	ld	(ix+#object.X+1),h
	ld	l,(ix+#object.Y+0)
	ld	h,(ix+#object.Y+1)
	add	hl,bc
	ld	(ix+#object.Y+0),l
	ld	(ix+#object.Y+1),h
	set	0,(ix+$18)
	ret

	}

	`--------------------------------------------------------------------[$5DEB]---

PROC	:_5deb {
	
	ld	hl,$0804
	ld	($.TEMP1),hl
	ld	a,($D414)
	and	$01
	jr	nz,._2
	ld	de,($.SONIC.X)
	ld	c,(ix+#object.X+0)
	ld	b,(ix+#object.X+1)
	ld	hl,$ffee
	add	hl,bc
	and	a
	sbc	hl,de
	jr	nc,._4
	ld	hl,$0010
	add	hl,bc
	and	a
	sbc	hl,de
	jr	c,._4
	ld	a,($D414)
	and	$04
	jr	nz,._1
	ld	l,(ix+#object.Y+0)
	ld	h,(ix+#object.Y+1)
	ld	a,($D40A)
	ld	c,a
	xor	a
	ld	b,a
	sbc	hl,bc
	ld	($.SONIC.Y+0),hl
	ld	($.D28E),a
	ld	a,($.D2E8)
	ld	hl,($.D2E6)
	ld	($.SONIC.Yspeed),hl
	ld	($.SONIC.Ydirection),a
	ld	hl,$D414
	set	7,(hl)
	scf	
	ret

._1	ld	a,($.SONIC.Ydirection)
	and	a
	jp	m,._3
	
._2	call	:_36be
	and	a
	ret

._3	ld	(ix+#object.Yspeed+0),$80
	ld	(ix+#object.Yspeed+1),$fe
	ld	(ix+#object.Ydirection),$ff
	ld	hl,$0400
	xor	a
	ld	($.SONIC.Yspeed),hl
	ld	($.SONIC.Ydirection),a
	ld	($.D28E),a
	set	1,(ix+$18)
	scf	
	ret

._4	ld	hl,($.SONIC.X)
	ld	de,$000c
	add	hl,de
	ex	de,hl
	ld	l,(ix+#object.X+0)
	ld	h,(ix+#object.X+1)
	ld	bc,$000a
	add	hl,bc
	ld	bc,$ffeb
	and	a
	sbc	hl,de
	jr	nc,._5
	ld	bc,$0015
._5	ld	l,(ix+#object.X+0)
	ld	h,(ix+#object.X+1)
	add	hl,bc
	ld	($.SONIC.X),hl
	xor	a
	ld	($D3FD),a
	ld	l,a
	ld	h,a
	ld	($.SONIC.Xspeed+0),a
	ld	($.SONIC.Xspeed+1),hl
	scf	
	ret

	}

	`____________________________________________________________________[$5EA2]___
	`OBJECT: chaos emerald	

PROC	:doObjectCode_powerUp_emerald {
	
	ld	hl,$D30B
	call	:getLevelBitFlag
	ld	a,(hl)
	and	c
	jr	nz,._1
	ld	(ix+#object.width),$0c
	ld	(ix+#object.height),$11
	call	:_5da8
	xor	a
	ld	(ix+#object.spriteLayout+0),a
	ld	(ix+#object.spriteLayout+1),a
	ld	hl,$0202
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	jr	c,._2
	ld	hl,$D30B
	call	:getLevelBitFlag
	ld	a,(hl)
	or	c
	ld	(hl),a
	ld	hl,$.D27F
	inc	(hl)
	ld	a,$fe
	ld	($.D28B),a
	
	ld	a,#index_music_emerald
	rst	:rst_playMusic
	
._1	ld	(ix+#object.type),$FF	`remove object?
	ret

._2	ld	a,($.FRAMECOUNT)
	rrca	
	jr	c,._3
	ld	(ix+#object.spriteLayout+0),._5f10.lo
	ld	(ix+#object.spriteLayout+1),._5f10.hi
._3	ld	l,(ix+#object.Yspeed+0)
	ld	h,(ix+#object.Yspeed+1)
	ld	a,(ix+#object.Ydirection)
	ld	de,$0020
	add	hl,de
	adc	a,$00
	ld	(ix+#object.Yspeed+0),l
	ld	(ix+#object.Yspeed+1),h
	ld	(ix+#object.Ydirection),a
	ld	hl,$5400		`$15400 - emerald in the blinking items art
	call	:loadPowerUpIcon
	ret

._5f10	DATA $5C, $5E, $FF, $FF, $FF, $FF
	DATA $FF

	}

}