`____________________________________________________________________________[$94A5]___
`OBJECT: UNKNOWN

PROC    :_94a5                                  SECTION ::objects
{
	set	5,(ix+#object.unknown18)
	ld	(ix+#object.width), 8
	ld	(ix+#object.height),10
	ld	hl,$0404
	ld	($.TEMP6),hl
	call	:_LABEL_3956_11
	call	nc,:_35fd
	bit	1,(ix+#object.unknown18)
	jr	nz,._1
	set	1,(ix+#object.unknown18)
	ld	hl,($.SONIC.X)
	ld	de,$000c
	add	hl,de
	ex	de,hl
	ld	l,(ix+#object.X.lo)
	ld	h,(ix+#object.X.hi)
	ld	bc,$0008
	add	hl,bc
	and	a
	sbc	hl,de
	jr	nc,._1
	set	2,(ix+#object.unknown18)
._1	bit	0,(ix+#object.unknown18)
	jr	nz,._3
	ld	(ix+#object.Yspeed.lo),$40
	ld	(ix+#object.Yspeed.hi),$00
	ld	(ix+#object.Ydirection),$00
	ld	hl,._9698
	bit	2,(ix+#object.unknown18)
	jr	z,._2
	ld	hl,._9688
._2	ld	(ix+#object.spriteLayout.lo),l
	ld	(ix+#object.spriteLayout.hi),h
	ld	hl,($.SONIC.Y)
	ld	e,(ix+#object.Y.lo)
	ld	d,(ix+#object.Y.hi)
	and	a
	sbc	hl,de
	ret	nc
	set	0,(ix+#object.unknown18)
	ret
        
        `------------------------------------------------------------------------------
	
._3	ld	c,(ix+#object.X.lo)
	ld	b,(ix+#object.X.hi)
	ld	hl,$fff0
	add	hl,bc
	ld	de,($.CAMERA_X)
	and	a
	sbc	hl,de
	jr	c,._4
	ld	l,c
	ld	h,b
	inc	d
	and	a
	sbc	hl,de
	jr	nc,._4
	ld	c,(ix+#object.Y.lo)
	ld	b,(ix+#object.Y.hi)
	ld	hl,$fff0
	add	hl,bc
	ld	de,($.CAMERA_Y)
	and	a
	sbc	hl,de
	jr	c,._4
	ld	hl,$00c0
	add	hl,de
	and	a
	sbc	hl,bc
	jr	nc,._5
._4	ld	(ix+#object.type),$FF	        `remove object
._5	xor	a;zero
	ld	hl,$0002
	bit	2,(ix+#object.unknown18)
	jr	nz,._6
	dec	a
	ld	hl,$fffe
._6	ld	e,(ix+#object.Xspeed.lo)
	ld	d,(ix+#object.Xspeed.hi)
	add	hl,de
	adc	a,(ix+#object.Xdirection)
	ld	c,a
	ld	a,h
	ld	de,$0100
	bit	7,c
	jr	z,._7
	ld	a,l
	cpl	
	ld	e,a
	ld	a,h
	cpl	
	ld	d,a
	inc	de
	ld	a,d
	ld	de,$ff00
._7	and	a
	jr	z,._8
	ex	de,hl
._8	ld	(ix+#object.Xspeed.lo),l
	ld	(ix+#object.Xspeed.hi),h
	ld	(ix+#object.Xdirection),c
	ld	hl,($.SONIC.Y)
	ld	de,$0010
	add	hl,de
	ex	de,hl
	ld	l,(ix+#object.Y.lo)
	ld	h,(ix+#object.Y.hi)
	ld	bc,$0008
	add	hl,bc
	and	a
	sbc	hl,de
	ld	a,$ff
	ld	hl,$fffe
	bit	7,(ix+#object.Ydirection)
	jr	nz,._9
	ld	hl,$fffc
._9	jr	nc,._10
	inc	a
	ld	hl,$0002
	bit	7,(ix+#object.Ydirection)
	jr	z,._10
	ld	hl,$0004
._10	ld	e,(ix+#object.Yspeed.lo)
	ld	d,(ix+#object.Yspeed.hi)
	add	hl,de
	adc	a,(ix+#object.Ydirection)
	ld	c,a
	ld	a,h
	ld	de,$0100
	bit	7,c
	jr	z,._11
	ld	a,l
	cpl	
	ld	e,a
	ld	a,h
	cpl	
	ld	d,a
	inc	de
	ld	a,d
	ld	de,$ff00
._11	and	a
	jr	z,._12
	ex	de,hl
._12	ld	(ix+#object.Yspeed.lo),l
	ld	(ix+#object.Yspeed.hi),h
	ld	(ix+#object.Ydirection),c
	ld	hl,._9688
	bit	7,(ix+#object.Xdirection)
	jr	z,._13
	ld	hl,._9698
._13	push	hl
	ld	l,(ix+#object.Xspeed.lo)
	ld	h,(ix+#object.Xspeed.hi)
	bit	7,h
	jr	z,._14
	ld	a,l
	cpl	
	ld	l,a
	ld	a,h
	cpl	
	ld	h,a
	inc	hl
._14	ld	e,(ix+#object.unknown11)
	ld	d,(ix+$12)
	add	hl,de
	ld	(ix+#object.unknown11),l
	ld	(ix+$12),h
	ld	a,h
	and	$08
	ld	e,a
	ld	d,$00
	pop	hl
	add	hl,de
	ld	(ix+#object.spriteLayout.lo),l
	ld	(ix+#object.spriteLayout.hi),h
	ld	l,(ix+#object.X.lo)
	ld	h,(ix+#object.X.hi)
	ld	de,$fff9
	bit	7,(ix+#object.Xdirection)
	jr	z,._15
	ld	de,$000f
._15	add	hl,de
	ld	($.TEMP1),hl
	ld	l,(ix+#object.Y.lo)
	ld	h,(ix+#object.Y.hi)
	ld	($.TEMP3),hl
	ld	a,($.FRAMECOUNT)
	and	$0f
	ret	nz
	call	:findEmptyObject
	ret	c
	push	ix
	push	hl
	pop	ix
	xor	a;zero			        `set A to 0
	ld	(ix+#object.type),$2A	        `unknown object
	ld	hl,($.TEMP1)
	ld	(ix+#object.Xsubpixel),a;zero
	ld	(ix+#object.X.lo),l
	ld	(ix+#object.X.hi),h
	ld	hl,($.TEMP3)
	ld	(ix+#object.Ysubpixel),a;zero
	ld	(ix+#object.Y.lo),l
	ld	(ix+#object.Y.hi),h
	ld	(ix+#object.unknown11),a;zero
	ld	(ix+$12),a;zero
	ld	(ix+#object.Xspeed.lo),a;zero
	ld	(ix+#object.Xspeed.hi),a;zero
	ld	(ix+#object.Xdirection),a;zero
	ld	(ix+#object.Yspeed.lo),a;zero
	ld	(ix+#object.Yspeed.hi),a;zero
	ld	(ix+#object.Ydirection),a;zero
	pop	ix
	ret

        `sprite layout
._9688  DATA $3C, $3E, $FF, $FF, $FF, $FF
        DATA $FF, $FF, $38, $3A, $FF, $FF
        DATA $FF, $FF, $FF, $FF
._9698  DATA $56, $58, $FF, $FF, $FF, $FF
        DATA $FF, $FF, $5A, $5C, $FF, $FF
        DATA $FF, $FF, $FF, $FF
}